apply plugin: 'com.android.application'


def VUFORIA_SDK_DIR = '../third-party/'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.3"

    // VUFORIA third party SDK needs to be in a directory called "third-party" in the root directory.
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir "$VUFORIA_SDK_DIR/build/lib/"
    }

    defaultConfig {
        applicationId "io.github.molecule.voice_record_replay_test"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        ndk {
            // Default ABI list for this app, can be over-ridden by providing an abiList property
            // e.g. gradle -PabiList=x86 clean assembleDebug
            abiFilters ((project.findProperty('abiList') ?: 'armeabi-v7a, arm64-v8a, x86').split(/,\s*/))
        }
    }
    archivesBaseName = rootProject.projectDir.getName()

    // Ensure that Vuforia Engine datasets placed in the assets folder do not get compressed as
    // this can significantly increase their loading time
    aaptOptions {
        noCompress 'dat'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Vuforia stuff obviously
    implementation files("$VUFORIA_SDK_DIR/build/java/Vuforia/Vuforia.jar")
    // ARCore library
    implementation 'com.google.ar:core:1.4.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
